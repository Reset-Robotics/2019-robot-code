import edu.wpi.first.gradlerio.GradleRIOPlugin
import edu.wpi.first.gradlerio.frc.FRCJavaArtifact
import edu.wpi.first.gradlerio.frc.RoboRIO

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.2.1"
    id "edu.wpi.first.GradleRIO" version "2019.1.1"
}

ext.kotlinVersion = "1.3"
ext.sertainVersion = "a2c505171b318836e0619b3c25dabae3a7857bf5"
ext.ktlintVersion = "0.19.0"


repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
}

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    compile "org.sert2521.sertain:core:$sertainVersion"

    //compile fileTree(include: ['*.jar'], dir: 'jars')

    ktlint "com.github.shyiko:ktlint:$ktlintVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

deploy {
    targets {
        target("roborio", RoboRIO) {
            team = getTeamOrDefault(6325)
        }
    }

    artifacts {
        artifact("main", FRCJavaArtifact) {
            targets << "roborio"

            debug = getDebugOrDefault(false)
        }
    }
}

wpi {
    wpilibVersion = "2019.1.1"
    ntcoreVersion = "4.1.0"
    opencvVersion = "3.2.0"
    cscoreVersion = "1.3.0"
    wpiutilVersion = "3.2.0"

    ctreVersion = "5.5.1.0"
    navxVersion = "3.0.348"

    shuffleboardVersion = "1.3.1"
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest("org.usfirst.frc.team6325.robot.Robot")
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

wrapper {
    gradleVersion = "5.0"
}